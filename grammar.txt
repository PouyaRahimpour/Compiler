program -> dec_list
dec_list -> dec dec_list # <eps>
dec -> type <id> init_dec
init_dec -> var_dec_global # func_dec

var_dec_global -> sz initial more 
more -> <,> var_dec_list <;> # <;>

var_dec -> type var_dec_list <;> # <;>
var_dec_list -> var_dec_init var_dec_list2 # <eps>
var_dec_list2 -> <,> var_dec_init var_dec_list2 # <eps>
var_dec_init -> var_id initial
var_id -> <id> sz
sz -> <[> optexp <]> # <eps>
optexp -> exp # <eps>

initial -> <=> eexp # <eps>
eexp -> exp1 # <{> exp_list <}>
exp_list -> exp1 exp_list2 # <eps>
exp_list2 -> <,> exp1 exp_list2 # <eps>
type -> <int> # <bool> # <char>

func_dec -> <(> params <)> <{> stmt_list <}>
params -> param params2 # <eps>
params2 -> <,> param params2 # <eps>
param -> type param_id
param_id -> <id> bracket 
bracket -> <[> <]> # <eps>

stmt_list -> stmt stmt_list # <eps>

stmt -> var_dec # break_stmt # continue_stmt # return_stmt # if_stmt # for_stmt # print_stmt # expr_stmt # <{> stmt_list <}> # <;>

if_stmt -> <if> <(> exp <)> stmt else_stmt
else_stmt -> <else> stmt # <eps>


break_stmt -> <break> <;>
continue_stmt -> <continue> <;>
return_stmt -> <return> exp <;>


for_stmt -> <for> <(> var_dec exp <;> exp <)> stmt
print_stmt -> <print> <(> context <)> <;>
context -> <string> context2
context2 -> <,> exp context2 # <eps>
expr_stmt -> exp <;>

exp -> exp1 t1 
t1 -> <,> exp1 t1 # <eps>

exp1 -> exp2 t2 
t2 -> <=> exp2 t2 # <eps>

exp2 -> exp3 t3
t3 -> <||> exp3 t3 # <eps>

exp3 -> exp4 t4
t4 -> <&&> exp4 t4 # <eps>

exp4 -> exp5 t5
t5 -> <==> exp5 t5 # <!=> exp5 t5 # <eps>

exp5 -> exp6 t6
t6 -> <<> exp6 t6 # <>> exp6 t6 # <<=> exp6 t6 # <>=> exp6 t6 # <eps>

exp6 -> exp7 t7
t7 -> <+> exp7 t7 # <-> exp7 t7 # <eps>

exp7 -> exp8 t8
t8 -> <*> exp8 t8 # </> exp8 t8 # <%> exp8 t8 # <eps>

exp8 -> <+> exp9 # <-> exp9 # exp9 # <!> exp9

exp9 -> <(> exp <)> # <id> func_call_or_id # constant

func_call_or_id -> <(> params_call <)> # ebracket 
params_call -> exp1 params_call2 # <eps>
params_call2 -> <,> exp1 params_call2 # <eps>
ebracket -> <[> exp <]> # <eps> 

constant -> <decimal> # <hexadecimal> # <string> # <character> # <false> # <true>
